_SECTION_BEGIN("B. Volume Flow");
// Downloaded From www.WiseStockTrader.com
//------------------------------------------------------------------------------
//  Formula Name  : OGTS Time Segmented Money Flow
//  Author        : KH Tang 
//  Company       : Wealth Resonance 
//  Last Update   : 15 Jan 2014
//  Date Updated  : 22 Nov 2013 // Update with opening price as direction*
//  Origin date	  : 5 Jun 2007 
//  Origin        : KH Tang 
//  Version       : 1.01
//  Level         : Volume Spread Analysis
//  Description   : Key Usages:
//                : 1.Tape Reading for Direction of Breakout in Trading Range
//                : 2.Tape Reading for Divergence in Trending
//	 	  : Note: Enhancement to include Opening price as direction.
//More notes on blog:
//http://blessedfool.blogspot.sg/2014/02/stock-market-tools-12-better-obv-and.html
//------------------------------------------------------------------------------


SetBarsRequired( -2, -2 );
//William C. Garrett's Approximation: 
//GPaths   = IIf(C>Ref(C,-1),((H-Ref(C,-1))+(H-L)+(C-L)),IIf(C<Ref(C,-1),(Ref(C,-1)-L)+(H-L)+(H-C),1));
//GUpShare = IIf(C>Ref(C,-1),((H-Ref(C,-1))+(C-L)),IIf(C<Ref(C,-1),(H-L),0));
//GDnShare = IIf(C>Ref(C,-1),(H-L),IIf(C<Ref(C,-1),(Ref(C,-1)-L)+(H-C),0));


//Expansion for clarity:
PathA1= Ref(C,-1)-L;
PathB1= H-L;
PathC1= H-C;
Path1 = PathA1+PathB1+PathC1;
UpShare1 = PathB1;
DnShare1 = PathA1+PathC1;

PathA2= H-Ref(C,-1);
PathB2= H-L;
PathC2= C-L;
Path2 = PathA2+PathB2+PathC2;
UpShare2 = PathA2+PathC2;
DnShare2 = PathB2;

Paths  = IIf(C<Ref(C,-1),Path1,	  IIf(C>Ref(C,-1),Path2,   1));
UpShare= IIf(C<Ref(C,-1),UpShare1,IIf(C>Ref(C,-1),UpShare2,0));
DnShare= IIf(C<Ref(C,-1),DnShare1,IIf(C>Ref(C,-1),DnShare2,0));

FlowFactor = Avg;

FlowIn  = UpShare/Paths*Volume*FlowFactor;
FlowOut = DnShare/Paths*Volume*FlowFactor;
NetFlow = FlowIn - FlowOut;
TotalFlow = Cum(NetFlow );


PlotType = ParamList("Type of Chart Plotting:","Cumulative|Time Segmented",1);
LBP = Param("LBP for Time Segmented Plot",26,10,50,1);
FastSmoothMF     = EMA(TotalFlow,int(LBP/2)); 
SlowSmoothMF   = EMA(TotalFlow,LBP);

DailyNetFlow= IIf(NetFlow==0,MA(Netflow,30),NetFlow);

if(PlotType=="Cumulative")
{
	TotalFlowColor = IIf(TotalFlow > Ref(TotalFlow,-1), colorGreen,colorRed);
	
	Plot(TotalFlow / 1000000 ,"Garrett Money Flow",TotalFlowColor,styleThick);
}
else if (PlotType=="Time Segmented")
{
	TSMF=(FastSmoothMF - SlowSmoothMF) / 1000000;
	TSMFsig = EMA(TSMF,9);
	Histogram = TSMF-TSMFSig;
	
	TSMF_Color = IIf(TSMF > Ref(TSMF,-1),colorGreen,colorRed);
	TSMFsig_Color = IIf(TSMFsig > Ref(TSMFsig,-1),colorBlue,colorRed);
	
	HistoRed    = (Histogram < Ref(Histogram,-1)) AND (Histogram < 0);
	HistoBlue   = (Histogram > Ref(Histogram,-1)) AND (Histogram < 0);
	HistoGreen  = (Histogram > Ref(Histogram,-1)) AND (Histogram > 0);
	HistoYellow   = (Histogram < Ref(Histogram,-1)) AND (Histogram > 0);
	Histogram_Color = IIf(HistoRed,colorRed,IIf(HistoBlue,colorBlue,IIf(HistoGreen,colorBrightGreen,IIf(HistoYellow,colorOrange,colorBlue))));

	
	
	Plot(TSMF,"Time Segmented Money Flow",Histogram_color,styleThick);
	Plot(0,"",colorBlack);
	Plot(TSMFsig,"Sig",TSMFsig_Color);
	Plot(Histogram,"Histogram",Histogram_Color,styleHistogram);
}
_SECTION_END();
